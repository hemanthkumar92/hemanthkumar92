//@version=5
indicator("Combined Indicator", overlay=true)

// Power Trend Indicator

length1 = 21
length2 = 30
recentHigh = ta.highest(close, 100)
ema1 = ta.ema(close, length1)
sma1 = ta.sma(close, length2)

plot(ema1, color=color.green, title="21-day EMA")
plot(sma1, color=color.red, title="30-day SMA")

var table myTable = table.new(position.bottom_right, 2, 3, bgcolor = color.black, border_width = 2, frame_width=1, frame_color = color.white, border_color=color.white)
var table myTable1 = table.new(position.top_right, 1, 2, bgcolor = color.black, border_width = 1, frame_width=1, frame_color = color.white, border_color=color.white)

table.cell(myTable, 0, 0, "EMA")
table.cell(myTable, 1, 0, "Line")
table.cell(myTable, 0, 1, "21 EMA")
table.cell(myTable, 1, 1, "─")
table.cell(myTable, 0, 2, "30 SMA")
table.cell(myTable, 1, 2, "─")

table.cell_set_text_color(myTable, 0, 0, color.white)
table.cell_set_text_color(myTable, 1, 0, color.white)
table.cell_set_text_color(myTable, 0, 1, color.white)
table.cell_set_text_color(myTable, 0, 2, color.white)
table.cell_set_text_color(myTable, 1, 1, color.green)
table.cell_set_text_color(myTable, 1, 2, color.red)

startCondition1 = math.sum(low > ema1 ? 1 : 0, 10) >= 10
startCondition2 = math.sum(ema1 > sma1 ? 1 : 0, 5) >= 5
startCondition3 = sma1 > sma1[1]
startCondition4 = close > open

var bool inPosition = false

startCondition = not inPosition and startCondition1 and startCondition2 and startCondition3 and startCondition4
endCondition = inPosition and (ta.crossover(sma1, ema1) or (close < 0.9 * recentHigh and close < sma1))

if (startCondition)
    inPosition := true

if (endCondition)
    inPosition := false

plotshape(startCondition, color=color.green, style=shape.labelup, location=location.belowbar, text="Trend On", textcolor = color.white)
plotshape(endCondition, color=color.red, style=shape.labeldown, location=location.abovebar, text="Trend Off", textcolor = color.white)

table.cell(myTable1, 0, 0, "Power Trend Status")
table.cell_set_text_color(myTable1, 0, 0, color.white)

if (inPosition)
    table.cell(myTable1, 0, 1, "●", text_color = color.green)
else 
    table.cell(myTable1, 0, 1, "●", text_color = color.red)

table.set_position(myTable1, position.middle_right)


// Chandelier Exit

var string calcGroup = 'Calculation'
length = input.int(title='ATR Period', defval=22, group=calcGroup)
mult = input.float(title='ATR Multiplier', step=0.1, defval=3.0, group=calcGroup)
useClose = input.bool(title='Use Close Price for Extremums', defval=true, group=calcGroup)

var string visualGroup = 'Visuals'
showLabels = input.bool(title='Show Buy/Sell Labels', defval=true, group=visualGroup)
highlightState = input.bool(title='Highlight State', defval=true, group=visualGroup)

var string alertGroup = 'Alerts'
awaitBarConfirmation = input.bool(title="Await Bar Confirmation", defval=true, group=alertGroup)

atr = mult * ta.atr(length)

longStop = (useClose ? ta.highest(close, length) : ta.highest(length)) - atr
longStopPrev = nz(longStop[1], longStop)
longStop := close[1] > longStopPrev ? math.max(longStop, longStopPrev) : longStop

shortStop = (useClose ? ta.lowest(close, length) : ta.lowest(length)) + atr
shortStopPrev = nz(shortStop[1], shortStop)
shortStop := close[1] < shortStopPrev ? math.min(shortStop, shortStopPrev) : shortStop

var int dir = 1
dir := close > shortStopPrev ? 1 : close < longStopPrev ? -1 : dir

var color longColor = color.green
var color shortColor = color.red
var color longFillColor = color.new(color.green, 90)
var color shortFillColor = color.new(color.red, 90)
var color textColor = color.new(color.white, 0)

longStopPlot = plot(dir == 1 ? longStop : na, title='Long Stop', style=plot.style_linebr, linewidth=2, color=color.new(longColor, 0))
buySignal = dir == 1 and dir[1] == -1
plotshape(buySignal ? longStop : na, title='Long Stop Start', location=location.absolute, style=shape.circle, size=size.tiny, color=color.new(longColor, 0))
plotshape(buySignal and showLabels ? longStop : na, title='Buy Label', text='Buy', location=location.absolute, style=shape.labelup, size=size.tiny, color=color.new(longColor, 0), textcolor=textColor)

shortStopPlot = plot(dir == 1 ? na : shortStop, title='Short Stop', style=plot.style_linebr, linewidth=2, color=color.new(shortColor, 0))
sellSignal = dir == -1 and dir[1] == 1
plotshape(sellSignal ? shortStop : na, title='Short Stop Start', location=location.absolute, style=shape.circle, size=size.tiny, color=color.new(shortColor, 0))
plotshape(sellSignal and showLabels ? shortStop : na, title='Sell Label', text='Sell', location=location.absolute, style=shape.labeldown, size=size.tiny, color=color.new(shortColor, 0), textcolor=textColor)

midPricePlot = plot(ohlc4, title='', style=plot.style_circles, linewidth=0, display=display.none, editable=false)

longStateFillColor = highlightState ? dir == 1 ? longFillColor : na : na
shortStateFillColor = highlightState ? dir == -1 ? shortFillColor : na : na
fill(midPricePlot, longStopPlot, title='Long State Filling', color=longStateFillColor)
fill(midPricePlot, shortStopPlot, title='Short State Filling', color=shortStateFillColor)
